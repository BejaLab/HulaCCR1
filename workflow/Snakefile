
from pandas import read_excel
import warnings

# all datasets as { dataset: basename } dict
datasets = dict(zip(*glob_wildcards("datasets/{dataset}/{basename}.fna")))

# all databases
databases = dict(zip(*glob_wildcards("databases/{database}/{basename}.pdb")))

# dataset metadata
with warnings.catch_warnings():
    warnings.simplefilter("ignore")
    metadata = read_excel("metadata/datasets.xlsx").set_index('analysis').T.to_dict('dict')

# reference PDB accession of HcKCR1 for alignment trimming
pdb_ref = "8H86"

wildcard_constraints:
    dataset = "|".join(datasets)

include: "Blastx.snakefile"
include: "Phylogeny.snakefile"
include: "Markers.snakefile"
include: "Flanks.snakefile"

rule all:
    input:
        expand("analysis/blastx/{dataset}/{basename}_ingroup.fna", zip, dataset = datasets, basename = datasets.values()),
        "output/clade_dist.csv",
        "output/pplacer.newick",
        "output/sintax.svg"

rule link_dataset:
    input:
        "datasets/{dataset}/{basename}.fna"
    output:
        "analysis/datasets/{dataset}_{basename}.fna"
    shell:
        "ln -sr {input} {output}"

rule faidx:
    input:
        "analysis/datasets/{dataset}_{basename}.fna"
    output:
        "analysis/datasets/{dataset}_{basename}.fna.fai"
    conda:
        "envs/kits.yaml"
    shell:
        "seqkit faidx {input}"
